
module Zurfur

use Zurfur[Copy]
use Zurfur.Operators[OpAdd, OpSub, OpCmp, OpOne]
use Zurfur.Collections[GetIterator, Iterator]


type pub ro copy Range<T>
        where T is Copy<T> + OpSub<T> + OpCmp<T> + OpOne<T>
    @begin T pub init get
    @end T pub init get

get pub (Range<T>) count() T
    return my.end-my.begin

get pub (Range<T>) iterator() RangeIterator<T>
    if my.begin >= my.end
        return  RangeIterator<T>(my.begin, my.begin)
    return RangeIterator<T>(my.begin, my.end)
        

type pub RangeIterator<T>
        where T is Copy<T> + OpSub<T> + OpCmp<T> + OpOne<T>
    @current T pub init
    @end T pub init
    
    
get pub (RangeIterator<T>) next() ?T
    if my.current == my.end
        return null
    @r = my.current
    my.current += T.ONE
    return r
        
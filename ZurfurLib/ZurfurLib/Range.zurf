
mod Zurfur

use Zurfur[Copy, Iterator, Number]
use Zurfur.Math[abs]

// TBD: Default constructor taking low and high
[pub]
type struct Range<T>
where T Number<T>
    low T
    high T

    [pub, implicit]
    fun get asIterator(my) RangeIterator<T>
        return RangeIterator<T>(my.low, my.high)


[pub]
fun Range.new<T>(my, low T, high T)
    // TBD: (r.low, r.high) = (low, high)
    my.low = low
    my.high = high

[pub]
fun get Range.length<T>(my) T // TBD: Use constraints from `Range`
where T Number<T>
    return my.high-my.low
        
[pub]
type RangeIterator<T>
where T Number<T>
    current T pub init
    high T pub init

[pub]
fun RangeIterator.new<T>(my, low T, high T)
    // TBD: (my.current, my.high) = (low, high)
    my.current = low
    my.high = high

[pub]
fun RangeIterator.next<T>(my mut) ?T
where T Number<T>
    if my.current >= my.high
        return nil
    @prev = my.current
    my.current += T.One
    return Maybe<T>(prev)
        
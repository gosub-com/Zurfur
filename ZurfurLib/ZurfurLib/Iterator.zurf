
mod Zurfur

[pub]
type interface GetIterator<T>
    fun self.getIterator() Iterator<T>

// Implement this on all iterators
[pub]
type interface Iterator<T>
    fun mut self.next() ?T


fun _for<S>(s GetIterator<S>) Iterator<S>
    ret s.getIterator
fun _for<S>(s Iterator<S>) Iterator<S>
    ret s


// *** MAP ***

fun (my GetIterator<S>) map<S,R>(f fun(inoput S) R) Iterator<R>
    ret my.getIterator.map(f)

fun (my Iterator<S>) map<S, R>(f fun(input S) R) Iterator<R>
    ret MapIterator<S, R>(my, f)
    
type ref MapIterator<S, R>
    _iter Iterator<S>
    _f fun(input S) R

// TBD: Remove this when `init` is implemented
fun (my MapIterator<S,R>) new<S, R>(iter Iterator<S>, f fun(input S) R)
    my._iter = iter
    my._f = f

fun (my mut MapIterator<S,R>) next<S, R>() ?R
    let a = my._iter.next()
    if a @ item
        ret my._f(a)
    ret nil

// *** FILTER ***

fun (my GetIterator<S>) filter<S>(f fun(inoput S) bool) Iterator<S>
    ret my.getIterator.filter(f)

fun (my Iterator<S>) filter<S>(f fun(input S) bool) Iterator<S>
    ret FilterIterator<S>(my, f)
    
type ref FilterIterator<S>
    _iter Iterator<S>
    _f fun(input S) bool

// TBD: Remove this when `init` is implemented
fun (my FilterIterator<S>) new<S>(iter Iterator<S>, f fun(input S) bool)
    my._iter = iter
    my._f = f

fun (my mut FilterIterator<S>) next<S>() ?S
    while my._iter.next @ item
        if my._f(item)
            ret item
    ret nil

// *** OTHER ***

fun (my GetIterator<S>) reduce<S,R>(acc R, f fun(acc R, value S) R) R
    ret my.getIterator.reduce<S,R>(acc, f)

fun (my Iterator<S>) reduce<S,R>(acc R, f fun(acc R, value S) R) R
    while my.next() @ item
        acc = f(acc, item)
    ret acc

fun (my GetIterator<S>) reduce<S>(f fun(item1 S, item2 S) S) ?S
    ret my.getIterator.reduce<S>(f)

fun (my Iterator<S>) reduce<S>(f fun(item1 S, item2 S) S) ?S
    if not (my.next()@acc)
        ret nil
    while my.next() @ item
        acc = f(acc, item)
    ret acc


[pub]
fun (my mut Iterator<T>) toList<T GetIterator<T>>() List<T>
    @list = List<T>()
    while my.next()@item
        list.push(item)
    ret list
    
    
    
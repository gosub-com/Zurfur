
mod Zurfur

[pub] type interface Iterable<T>
    fun asIterator(my) Iterator<T>

// Implement this on all iterators
[pub] type interface Iterator<T>
    fun next(my mut) ?T


// This is never called.  It is implemented by the compiler to generate a for loop.
fun Iterator._for<T>(my) T extern


// *** MAP ***

fun Iterator.map<S, R>(my Iterator<S>, f fun(input S) R) Iterator<R>
    return MapIterator<S, R>(my, f)
    
type ref MapIterator<S, R>
    _iter Iterator<S>
    _f fun(input S) R

// TBD: Remove this when `init` is implemented
fun MapIterator.new<S, R>(my, iter Iterator<S>, f fun(input S) R)
    my._iter = iter
    my._f = f

fun MapIterator.next<S, R>(my mut) ?R
    let a = my._iter.next()
    if a @ item
        return my._f(a)
    return nil

// *** FILTER ***

fun Iterator.filter<S>(my, f fun(input S) Bool) Iterator<S>
    return FilterIterator<S>(my, f)
    
type ref FilterIterator<S>
    _iter Iterator<S>
    _f fun(input S) Bool

// TBD: Remove this when `init` is implemented
fun FilterIterator.new<S>(my, iter Iterator<S>, f fun(input S) Bool)
    my._iter = iter
    my._f = f

fun FilterIterator.next<S>(my mut) ?S
    while my._iter.next @ item
        if my._f(item)
            return item
    return nil

// *** OTHER ***


fun Iterator.reduce<S,R>(my Iterator<S>, acc R, f fun(acc R, value S) R) R
    while my.next() @ item
        acc = f(acc, item)
    return acc

fun Iterator.reduce<S>(my, f fun(item1 S, item2 S) S) ?S
    if not (my.next()@acc)
        return nil
    while my.next() @ item
        acc = f(acc, item)
    return acc


[pub]
fun Iterator.toList<T>(my mut) List<T>
where T Iterable<T>
    @list = List<T>()
    while my.next()@item
        list.push(item)
    return list
    
    
    
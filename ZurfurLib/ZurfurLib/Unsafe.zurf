

mod Zurfur.Unsafe

use Zurfur[RawPointerMarker, Type]

// Mark these types as pointers and allow castPointer
fun Xuint.getRawPointer(my) RawPointerMarker extern
fun Ref.getRawPointer<T>(my) RawPointerMarker extern
fun RawPointer.getRawPointer<T>(my) RawPointerMarker extern

// Cast from one pointer type to another pointer type.
[pub]
fun castPointer<T>(i GetRawPointer) T
where T GetRawPointer
extern

// Unsigned extended Int, U32 or U64 depending on machine pointer size.
// This is the internal representation of a pointer.
[pub]
type ro Xuint
    value U32 // or U64

fun Xuint.new(my, value U64) 
    assert(value <= (0U32).MaxValue.toU64)
    my.value = value.toU32Mask

// Internal representation of `ref`, a compiler generated stack only pointer.
// The first version is a reference counting implementation, but future
// versions might not be.  The owner is nil when a non-heap (static global or
// stack local) object is referenced.  These references are also called return
// references because they get passed back up the stack.
type ref ro Ref<T>
    owner ?*GcHeapObject
    value *T


[pub] fun Ref.new<T>(my, p *T) todo

// When implemented, the compiler knows it is a pointer (32 or 64 bits)
// and can allow `castPointer`.  `RawPointerMarker` is private, so only
// pointer types can implement this.
type interface GetRawPointer
    fun getRawPointer(my) RawPointerMarker

// A raw pointer.  The compiler translates *T to RawPointer<T>
// and automatically dereferences when "." is used.
[pub]
type ro RawPointer<T>
    value Xuint

    [pub, implicit] fun get toVoid(my)*Void extern
    [pub, implicit, static] fun toPointer(value Nil)*T extern

[pub] fun _opEq<T>(a RawPointer<T>, b RawPointer<T>) Bool extern
[pub] fun _opCmp<T>(a RawPointer<T>, b RawPointer<T>) Int extern
[pub] fun _opAdd<T>(a RawPointer<T>, b U64) RawPointer<T> extern
[pub] fun _opAdd<T>(a U64, b RawPointer<T>) RawPointer<T> extern
[pub] fun _opSub<T>(a RawPointer<T>, b  RawPointer<T>) RawPointer<T> extern
[pub] fun _opSub<T>(a RawPointer<T>, b U64) RawPointer<T> extern
[pub] fun _opAdd<T>(a RawPointer<T>, b Int) *T extern
[pub] fun _opAdd<T>(a Int,b *T) *T extern


// TBD: Move to a different modulem probably Zurfur.Internal
// All heap objects start with this header.  It must be 64 bits wide to
// ensure proper alignment. 
type GcHeapObject
    refCount I32
    typeId Type     // Must be 32 bits even on 64 bit machines.
    // Object data is stored following this header

// TBD: Move to a different modulem probably Zurfur.Internal
// The one and only variable length heap object.  It must be 128 bits
// wide to ensure proper alignment.
type GcHeapArray
    // TBD: use GcHeapObject
    //NOTE: Until we get 'use', the first two fields MUST be the same
    //      as GcHeapObject since we will cast pointers back and forth
    refCount I32
    typeId Type
    length U32
    capacity U32
    // Array of object data is stored following this header
    
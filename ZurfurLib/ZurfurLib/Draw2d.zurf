
// Basic shapes for use with 2d drawing library.
// Point, Size, Rect.
// TBD: Vec2, Vec3, Vec4 for 3d, is Point same as Vec2?
mod Zurfur.Draw2d

use Zurfur[Number]

fun test_Shape()
    @rect = Rect<int>(1,2,3,4) // TBD: Type inderence for constructors
    
    rect.location.y = 30
    @p1 = rect.location
    @rly1 = rect.location.y
    @rly2 = rect.location.x2
    @p2 = p1 + Point(1,2)
    @p3 = 2*p1
    @p4 = p1*2
    
    

// TBD: Infer constraint on functions below from Point<T>
//      Default constructor taking both x and y
[pub]
type struct Point<T Number<T>>
    x T
    y T

// Identical to a field
fun get (my Point<T>) x2<T>() mut &T
    ret &my.x // TBD: Require `&`, do not allow just `my.x`

[pub]
type struct Size<T Number<T>>
    width T
    height T

[pub]
type struct Rect<T Number<T>>
    // TBD: use location Point<T>
    //      use size Size<T>
    location Point<T>
    size Size<T>

// Identical to fields (not necessary when we have 'use location Point<T>' syntax)
[pub]
fun get (my Rect<T>) x<T>() mut &T
    ret &my.location.x // TBD: Require `&`
[pub]
fun get (my Rect<T>) y<T>() mut &T
    ret &my.location.y // TBD: Require `&`
[pub]
fun get (my Rect<T>) width<T>() mut &T
    ret &my.size.height // TBD: Require `&`
[pub]
fun get (my Rect<T>) height<T>() mut &T
    ret &my.size.width // TBD: Require `&`


[pub]
fun (my Point<T>) new<T>(x T, y T)
    // TBD: (my.x, my.y) = (x, y)
    my.x = x
    my.y = y


[pub]
fun (my Point<T>) toSize<T>() Size<T>
    ret Size<T>(my.x, my.y)

// TBD: Remove
fun (my Point<T>) yf<T>() T
    ret my.y


// TBD: Infer constraint from Point<T>
[pub]
fun _opAdd<T Number<T>>(a Point<T>, b Point<T>) Point<T>
    ret Point(a.x+b.x, a.y+b.y)

[pub]
fun _opNeg<T Number<T>>(a Point<T>) Point<T>
    ret Point(-a.x, -a.y)

[pub]
fun _opSub<T Number<T>>(a Point<T>, b Point<T>) Point<T>
    ret Point(a.x-b.x, a.y-b.y)

[pub]
fun _opMul<T Number<T>>(a T, b Point<T>) Point<T>
    ret Point(a*b.x, a*b.y)

[pub]
fun _opMul<T Number<T>>(a Point<T>, b T) Point<T>
    ret Point(a.x*b, a.y*b)

[pub]
fun _opDiv<T Number<T>>(a T, b Point<T>) Point<T>
    ret Point(a/b.x, a/b.y)

[pub]
fun _opDiv<T Number<T>>(a Point<T>, b T) Point<T>
    ret Point(a.x/b, a.y/b)

[pub]
fun (my Size<T>) new<T>(width T, height T)
    // TBD: (my.width, my.height) = (width, height)
    my.width = width
    my.height = height
    
[pub]
fun (my Size<T>) toPoint<T>() Point<T>
    ret Point(my.width, my.height)


// TBD: Shortcut syntax for this:
[pub]
fun (my Rect<T>) new<T>(x T, y T, width T, height T)
    my.x = x
    my.y = y
    my.width = width
    my.height = height
    
[pub]
fun (my Rect<T>) new<T>(location Point<T>, size Size<T>)
    my.location = location
    my.size = size


// TBD: Operators (add, multiply, divide, sizes, etc.)

[pub]
fun get (my Rect<T>) left<T>() T
    ret my.x
    
[pub]
fun get (my Rect<T>) right<T Number<T>>() T // TBD: Use constraints from `Rect`
    ret my.x + my.width

[pub]
fun get (my Rect<T>) top<T>() T
    ret my.y
    
[pub]
fun get (my Rect<T>) bottom<T Number<T>>() T // TBD: Use constraints from `Rect`
    ret my.y + my.height
    
    
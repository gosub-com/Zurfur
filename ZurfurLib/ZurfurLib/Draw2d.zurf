
// Basic shapes for use with 2d drawing library.
// Point, Size, Rect.
// TBD: Vec2, Vec3, Vec4 for 3d, is Point same as Vec2?
mod Zurfur.Draw2d

use Zurfur[Number]

fun test_Shape()
    @rect = Rect(1,2,3,4)
    
    rect.location.y = 30
    @p1 = rect.location
    @rly1 = rect.location.y
    @rly2 = rect.location.x2
    @p2 = p1 + Point(1,2)
    @p3 = 2*p1
    @p4 = p1*2
    
    


[pub]
type struct Point<T>
where T Number<T>
    x T
    y T

// Identical to a field
fun get Point.x2<T>(my) mut &T
    return &my.x // TBD: Require `&`, do not allow just `my.x`

[pub]
type struct Size<T>
where T Number<T>
    width T
    height T

[pub]
type struct Rect<T>
where T Number<T>
    // TBD: use location Point<T>
    //      use size Size<T>
    location Point<T>
    size Size<T>

// Identical to fields (not necessary when we have 'use location Point<T>' syntax)
[pub]
fun get Rect.x<T>(my) mut &T
    return &my.location.x // TBD: Require `&`
    
[pub]
fun get Rect.y<T>(my) mut &T
    return &my.location.y // TBD: Require `&`
    
[pub]
fun get Rect.width<T>(my) mut &T
    return &my.size.height // TBD: Require `&`
    
[pub]
fun get Rect.height<T>(my) mut &T
    return &my.size.width // TBD: Require `&`
    


[pub]
fun Point.new<T>(my, x T, y T)
    // TBD: (my.x, my.y) = (x, y)
    my.x = x
    my.y = y


[pub]
fun Point.toSize<T>(my) Size<T>
    return Size<T>(my.x, my.y)

// TBD: Remove
fun Point.yf<T>(my) T
    return my.y


[pub]
fun _opAdd<T>(a Point<T>, b Point<T>) Point<T>
where T Number<T>
    return Point(a.x+b.x, a.y+b.y)

[pub]
fun _opNeg<T>(a Point<T>) Point<T>
where T Number<T>
    return Point(-a.x, -a.y)

[pub]
fun _opSub<T>(a Point<T>, b Point<T>) Point<T>
where T Number<T>
    return Point(a.x-b.x, a.y-b.y)

[pub]
fun _opMul<T>(a T, b Point<T>) Point<T>
where T Number<T>
    return Point(a*b.x, a*b.y)

[pub]
fun _opMul<T>(a Point<T>, b T) Point<T>
where T Number<T>
    return Point(a.x*b, a.y*b)

[pub]
fun _opDiv<T>(a T, b Point<T>) Point<T>
where T Number<T>
    return Point(a/b.x, a/b.y)

[pub]
fun _opDiv<T>(a Point<T>, b T) Point<T>
where T Number<T>
    return Point(a.x/b, a.y/b)

[pub]
fun Size.new<T>(my, width T, height T)
    // TBD: (my.width, my.height) = (width, height)
    my.width = width
    my.height = height
    
[pub]
fun Size.toPoint<T>(my) Point<T>
    return Point(my.width, my.height)



[pub]
fun Rect.new<T>(my, x T, y T, width T, height T)
    my.x = x
    my.y = y
    my.width = width
    my.height = height
    
[pub]
fun Rect.new<T>(my, location Point<T>, size Size<T>)
    my.location = location
    my.size = size


// TBD: Operators (add, multiply, divide, sizes, etc.)

[pub]
fun get Rect.left<T>(my) T
    return my.x
    
[pub]
fun get Rect.right<T>(my) T // TBD: Use constraints from `Rect`
where T Number<T>
    return my.x + my.width

[pub]
fun get Rect.top<T>(my) T
    return my.y
    
[pub]
fun get Rect.bottom<T>(my) T // TBD: Use constraints from `Rect`
where T Number<T>
    return my.y + my.height
    
    
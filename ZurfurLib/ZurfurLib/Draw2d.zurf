
// Basic shapes for use with 2d drawing library.
// Point, Size, Rect.
// TBD: Vec2, Vec3, Vec4 for 3d, is Point same as Vec2?
mod Zurfur.Draw2d

use Zurfur[NumOps]

fun test_Shape()
    @rect = Rect<int>(1,2,3,4) // TBD: Type inderence for constructors
    
    rect.location.y = 30
    @p1 = rect.location
    @rly1 = rect.location.y
    @rly2 = rect.location.x2
    @p2 = p1 + Point(1,2)
    @p3 = 2*p1
    @p4 = p1*2
    
    

[pub]
type Point<T> copy
    where T has NumOps<T>
    x T
    y T

// Identical to a field
fun get Point.x2<T>() mut &T
    ret &my.x // TBD: Require `&`, do not allow just `my.x`

[pub]
type Size<T> copy
    where T has NumOps<T>
    
    width T
    height T

[pub]
type Rect<T> copy
    where T has NumOps<T>
    
    // TBD: use location Point<T>
    //      use size Size<T>
    location Point<T>
    size Size<T>

// Identical to fields (not necessary when we have 'use location Point<T>' syntax)
[pub]
fun get Rect.x<T>() mut &T
    ret &my.location.x // TBD: Require `&`
[pub]
fun get Rect.y<T>() mut &T
    ret &my.location.y // TBD: Require `&`
[pub]
fun get Rect.width<T>() mut &T
    ret &my.size.height // TBD: Require `&`
[pub]
fun get Rect.height<T>() mut &T
    ret &my.size.width // TBD: Require `&`


[pub]
fun Point.new<T>(x T, y T)
    // TBD: (my.x, my.y) = (x, y)
    my.x = x
    my.y = y


[pub]
fun Point.toSize<T>() Size<T>
    ret Size<T>(my.x, my.y)

// TBD: Remove
fun Point.yf<T>() T
    ret my.y


[pub]
fun _opAdd<T>(a Point<T>, b Point<T>) Point<T>
    where T has NumOps<T>
    ret Point(a.x+b.x, a.y+b.y)

[pub]
fun _opNeg<T>(a Point<T>) Point<T>
    where T has NumOps<T>
    ret Point(-a.x, -a.y)

[pub]
fun _opSub<T>(a Point<T>, b Point<T>) Point<T>
    where T has NumOps<T>
    ret Point(a.x-b.x, a.y-b.y)

[pub]
fun _opMul<T>(a T, b Point<T>) Point<T>
    where T has NumOps<T>
    ret Point(a*b.x, a*b.y)

[pub]
fun _opMul<T>(a Point<T>, b T) Point<T>
    where T has NumOps<T>
    ret Point(a.x*b, a.y*b)

[pub]
fun _opDiv<T>(a T, b Point<T>) Point<T>
    where T has NumOps<T>
    ret Point(a/b.x, a/b.y)

[pub]
fun _opDiv<T>(a Point<T>, b T) Point<T>
    where T has NumOps<T>
    ret Point(a.x/b, a.y/b)

[pub]
fun Size.new<T>(width T, height T)
    // TBD: (my.width, my.height) = (width, height)
    my.width = width
    my.height = height
    
[pub]
fun Size.toPoint<T>() Point<T>
    ret Point(my.width, my.height)


// TBD: Shortcut syntax for this:
[pub]
fun Rect.new<T>(x T, y T, width T, height T)
    my.x = x
    my.y = y
    my.width = width
    my.height = height
    
[pub]
fun Rect.new<T>(location Point<T>, size Size<T>)
    my.location = location
    my.size = size


// TBD: Operators (add, multiply, divide, sizes, etc.)

[pub]
fun get Rect.left<T>() T
    ret my.x
    
[pub]
fun get Rect.right<T>() T
    where T has NumOps<T> // TBD: Use constraints from `Point`
    ret my.x + my.width

[pub]
fun get Rect.top<T>() T
    ret my.y
    
[pub]
fun get Rect.bottom<T>() T
    where T has NumOps<T> // TBD: Use constraints from `Point`
    ret my.y + my.height
    
    